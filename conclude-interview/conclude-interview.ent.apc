{Application 'ADD_ANOTHER_PERSON' logic file generated by CSPro}
PROC GLOBAL

PROC HOUSEHOLD_FF

PROC HH_NUMBER

preproc

// TODO: Prefill interviewer and supervisor codes with information passed in from menu
// HH_INTERVIEWER_CODE = sysparm("InterviewerCode");
// HH_SUPERVISOR_CODE = sysparm("SupervisorCode");

if HH_START_INTERVIEW_TIME = notappl then
	// Will only be prefilled once
	HH_START_INTERVIEW_TIME = timestamp();
endif;


PROC HH_POPULATION_SECTION

// Adding this field gives us a destination to reenter from HH_CONCLUDE_INTERVIEW

preproc

// Prefill response
$ = 1;


PROC HH_NAME

postproc

if HH_NAME = "" then
	errmsg("Name cannot be blank");
	reenter;
endif;


PROC HH_ADD_ANOTHER

preproc

savepartial();

onfocus

// From add-another example
if curocc() < maxocc(HOUSEHOLD_ROSTER) and HH_NAME(curocc() + 1) <> "" then
	setproperty(HH_ADD_ANOTHER, "Protected", "Yes");
	HH_ADD_ANOTHER = 1;

else
	setproperty(HH_ADD_ANOTHER, "Protected", "No");

endif;

postproc

// From add-another example
if HH_ADD_ANOTHER = 2 then
	endgroup;
endif;
	
	
PROC HH_AGE

preproc

// From add-another example
if curocc() > totocc(HOUSEHOLD_ROSTER) then
	endgroup;
endif;


PROC HH_CONCLUDE_INTERVIEW

preproc

// Clear out response so that interviewer always have to manually accept the case
HH_CONCLUDE_INTERVIEW = notappl;

postproc

if HH_CONCLUDE_INTERVIEW = 1 then
	// The program will close after this field
	HH_END_INTERVIEW_TIME = timestamp();

elseif HH_CONCLUDE_INTERVIEW = 2 then
	// Allows review of population section
	reenter HH_POPULATION_SECTION;
	
endif;
