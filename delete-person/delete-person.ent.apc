{Application 'ADD_ANOTHER_PERSON' logic file generated by CSPro}
PROC GLOBAL

function ShowHidePopulation()
	// Set name as occurrence label. If row has been deleted then set occurrence label as <Deleted>.
	// Function is called in three difference places. Search source for each function call.

	do numeric ctr = 1 while ctr <= maxocc(HOUSEHOLD_ROSTER) and visualvalue(HH_DELETE_ROW(ctr)) <> notappl

		numeric showRow = 0;
		if visualvalue(HH_DELETE_ROW(ctr)) = 2 then
			showRow = 1;
		endif;

		string populationLabel;

		if showRow then
			populationLabel = HH_NAME(ctr);

		else
			populationLabel = "<Deleted>";

		endif;

		showocc(HOUSEHOLD_ROSTER(ctr), showRow);
		showocc(DEMOGRAPHIC_ROSTER(ctr), showRow);
		setocclabel(HOUSEHOLD_ROSTER(ctr), populationLabel);
		setocclabel(DEMOGRAPHIC_ROSTER(ctr), populationLabel);

	enddo;

end;

PROC HOUSEHOLD_FF

PROC HOUSEHOLD_LEVEL

preproc

// In case we come back from partial save or modify mode, do this after loading the questionnaire
ShowHidePopulation();


PROC HH_DELETE_ROW

preproc

if visualvalue(HH_DELETE_ROW) = notappl or curocc() = 1 then
	// Initialize when entering row for 1st time
	HH_DELETE_ROW = 2;
	noinput;
endif;

onfocus

if visualvalue(HH_DELETE_ROW) = 1 then
	// (cont.) once a row is deleted, it can't be modified
	setproperty(HH_DELETE_ROW, "Protected", "Yes");
	
else
	setproperty(HH_DELETE_ROW, "Protected", "No");
	
endif;

postproc

	// Set occurrence label after person has been deleted
	ShowHidePopulation();

	if HH_DELETE_ROW = 1 then

		// Row is deleted
		if curocc() = 1 then
			errmsg("You cannot delete the first row.");
			HH_DELETE_ROW = 2;
			
		else
			skip to HH_ADD_ANOTHER;
			
		endif;
		
	endif;

	
PROC HH_NAME

postproc

if HH_NAME = "" then
	errmsg("Name cannot be blank");
	reenter;
endif;

// Set occurrence label after name has been entered
ShowHidePopulation();


PROC HH_ADD_ANOTHER

preproc

savepartial();

onfocus

// From add-another example
if curocc() < maxocc(HOUSEHOLD_ROSTER) and HH_NAME(curocc() + 1) <> "" then
	setproperty(HH_ADD_ANOTHER, "Protected", "Yes");
	HH_ADD_ANOTHER = 1;

else
	setproperty(HH_ADD_ANOTHER, "Protected", "No");

endif;

postproc

// From add-another example
if HH_ADD_ANOTHER = 2 then
	endgroup;
endif;
	
	
PROC HH_AGE

preproc

if HH_DELETE_ROW = 1 then
	// The row has been deleted. Skip to the person.
	skip to next;

// From add-another example
elseif curocc() > totocc(HOUSEHOLD_ROSTER) then
	endgroup;
	
endif;
